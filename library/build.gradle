apply plugin: 'com.android.library'

android {
    compileSdkVersion Integer.parseInt(pCompileSdkVersion)
    buildToolsVersion pBuildToolsVersion

    defaultConfig {
        minSdkVersion Integer.parseInt(pMinSdkVersion)
        targetSdkVersion Integer.parseInt(pCompileSdkVersion)
        versionCode 1
        versionName pVersionName
        consumerProguardFiles 'proguard-consumer.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.pro'
        }
    }

    libraryVariants.all { variant ->
        int versionCode = 0
        (pVersionName =~ /(\d+)/).collect {
            Integer.parseInt(it[0] as String)
        }.take(4).eachWithIndex { num, index ->
            versionCode += (num * Math.max(1, (1000000 / Math.pow(100, index))))
        }
        variant.mergedFlavor.versionCode = versionCode
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions { unitTests { all { jvmArgs '-noverify' } } }
}

dependencies {
    provided project(':bindings')
    compile "com.android.support:support-annotations:${pSupportVersion}"
    compile "io.reactivex:rxjava:${pRxJavaVersion}"
    testCompile "junit:junit:${pJUnitVersion}"
    testCompile "org.mockito:mockito-core:${pMockitoVersion}"
    testCompile "org.hamcrest:java-hamcrest:${pHamcrestVersion}"
}

if (project.hasProperty('bintrayUser')) {
    apply from: 'bintray.gradle'
}